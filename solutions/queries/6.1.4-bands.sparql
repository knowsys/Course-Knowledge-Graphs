# -*- mode: sparql -*-
#title:bands with the most former members
SELECT ?band ?bandLabel (COUNT(DISTINCT ?person) AS ?formerMembers) WHERE {
  ?band wdt:P31/wdt:P279* wd:Q215380 ;       # a band
        p:P527|^ps:P463 ?stmt .              # has-part or member-of
  ?stmt pq:P582 [] ;                         # end time
        ps:P527|^p:P463 ?person .            # has-part or member-of
  FILTER NOT EXISTS { ?person wdt:P570 [] }  # no date of death
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
  } GROUP BY ?band ?bandLabel ORDER BY DESC(?formerMembers) LIMIT 20
