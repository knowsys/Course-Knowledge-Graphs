# -*- mode: sparql -*-
#title:universities employing professers educated there
SELECT ?universityLabel (COUNT(DISTINCT ?person) AS ?persons) WHERE {
  ?person wdt:P31/wdt:P279* wd:Q5 ;         # a human
          wdt:P106 wd:Q121594 ;             # occupation: professor
          wdt:P69 ?university ;             # educated at: university
          wdt:P108 ?university .            # employer: university
  ?university wdt:P31/wdt:P279* wd:Q3918 .  # a university
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
  } GROUP BY ?universityLabel ORDER BY DESC(?persons) LIMIT 25
