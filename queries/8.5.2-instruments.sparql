# -*- mode: sparql -*-
#title:instruments played by composers
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?instrument ?instrumentLabel (COUNT(DISTINCT ?person) AS ?composers) WHERE {
  ?instrument ^wdt:P1303 ?person . # instrument-of: person
  {{ ?person wdt:P106 wd:Q36834    # occupation: composer
  } UNION { ?person ^wdt:P86 []    # composed-by: person
  }}
  ?instrument rdfs:label ?instrumentLabel .
  FILTER(LANG(?instrumentLabel) = "en")
} GROUP BY ?instrument ?instrumentLabel ORDER BY DESC(?composers) LIMIT 10
