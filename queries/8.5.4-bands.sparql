# -*- mode: sparql -*-
#title:bands with the most former members
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?band ?bandLabel (COUNT(DISTINCT ?person) AS ?formerMembers) WHERE {
  ?band wdt:P31/wdt:P279* wd:Q215380 ;       # a band
        p:P527|^ps:P463 ?stmt .              # has-part or member-of
  ?stmt pq:P582 [] ;                         # end time
        ps:P527|^p:P463 ?person .            # has-part or member-of
  FILTER NOT EXISTS { ?person wdt:P570 [] }  # no date of death
  ?band rdfs:label ?bandLabel .
  FILTER(LANG(?bandLabel) = "en")
} GROUP BY ?band ?bandLabel ORDER BY DESC(?formerMembers) LIMIT 20
