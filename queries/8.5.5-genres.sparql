# -*- mode: sparql -*-
#title:musical genres with the most bands and musicians
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?genre ?genreLabel ?performers WHERE {
{ SELECT ?genre (COUNT(DISTINCT ?performer) AS ?performers) WHERE {
    ?genre wdt:P31/wdt:P279* wd:Q188451 ;                       # a genre
           ^wdt:P136 ?performer .                               # genre-of: performer
    {{ ?performer wdt:P31/wdt:P279* wd:Q215380 } UNION          # performer a band
     { ?performer ((wdt:P31/wdt:P279*)|wdt:P106) wd:Q639669 }}  # â€¦ or a musician or occupation: musician
    } GROUP BY ?genre ORDER BY DESC(?performers) LIMIT 42
  }
  ?genre rdfs:label ?genreLabel .
  FILTER(LANG(?genreLabel) = "en")
} ORDER BY DESC(?performers)
